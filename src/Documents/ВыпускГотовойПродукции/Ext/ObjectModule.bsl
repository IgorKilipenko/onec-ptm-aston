#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(_, __)
    выполнитьВсеДвиженияДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции

Процедура выполнитьВсеДвиженияДокумента()
    ЭтотОбъект.Движения.ТоварыНаСкладах.Записывать = Истина;
    выполнитьВсеДвиженияТоваровДокумента();
КонецПроцедуры

Процедура выполнитьВсеДвиженияТоваровДокумента()
    таблицаТоварыДокумента = Документы.ВыпускГотовойПродукции.ПолучитьТоварыДокумента(ЭтотОбъект.Ссылка, Истина);
    Если таблицаТоварыДокумента = Неопределено Тогда // Если таблица не заполнена
        Возврат;
    КонецЕсли;

    Для Каждого строкаТоваров Из таблицаТоварыДокумента Цикл
        выполнитьДвижениеТоварыНаСкладахПриход(строкаТоваров);
    КонецЦикла;
КонецПроцедуры

// Параметры:
//  строкаТоваров - СтрокаТаблицыЗначений, ВыборкаИзРезультатаЗапроса, Структура
//      * Номенклатура - СправочникСсылка.Номенклатура
//      * Количество - Число
Процедура выполнитьДвижениеТоварыНаСкладахПриход(Знач строкаТоваров)
    движение = ЭтотОбъект.Движения.ТоварыНаСкладах.Добавить();
    движение.ВидДвижения = ВидДвиженияНакопления.Приход;
    движение.Период = ЭтотОбъект.Дата;
    движение.Склад = ЭтотОбъект.Склад;
    движение.Номенклатура = строкаТоваров.Номенклатура;
    движение.Количество = строкаТоваров.Количество;
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
