#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(_)
    ткущаяСтрокаНоменклатуры = ЭтотОбъект.Элементы.Товары.ТекущиеДанные;
    обновитьЦенуНоменклатуры(ткущаяСтрокаНоменклатуры);
    обновитьСуммуПоТекущейСтрокеНоменклатуры(ткущаяСтрокаНоменклатуры);
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(_)
    ткущаяСтрокаНоменклатуры = ЭтотОбъект.Элементы.Товары.ТекущиеДанные;
    обновитьСуммуПоТекущейСтрокеНоменклатуры(ткущаяСтрокаНоменклатуры);
    обновитьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(_)
    обновитьСуммуДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовТаблицыФормыТовары

#Область СлужебныеПроцедурыИФункции

// Устанавливает цену номенклатуры для текущей строки товаров
// Параметры:
//  ткущаяСтрокаНоменклатуры - ДанныеФормыСтруктура
&НаКлиенте
Процедура обновитьЦенуНоменклатуры(Знач ткущаяСтрокаНоменклатуры)
    Если ЗначениеЗаполнено(ткущаяСтрокаНоменклатуры.Номенклатура) = Ложь Тогда
        ткущаяСтрокаНоменклатуры.Цена = 0;
        Возврат;
    КонецЕсли;

    ценаНоменклатуры = получитьЦенуНоменклатуры(ткущаяСтрокаНоменклатуры.Номенклатура);
    ткущаяСтрокаНоменклатуры.Цена = ?(ценаНоменклатуры = Неопределено, 0, ценаНоменклатуры);

    Если ценаНоменклатуры = Неопределено Тогда
        сообщение = Новый СообщениеПользователю;
        сообщение.Текст = СтрШаблон(
                "Не удалось определить цену номенклатуры ""%1"".
                |Отсутствует информация в регистре цен номенклатуры по указанной позиции.", ткущаяСтрокаНоменклатуры.Номенклатура);
        сообщение.Сообщить();
    КонецЕсли;
КонецПроцедуры

// Выполнят расчет стоимости по текущей строке товаров
// Параметры:
//  ткущаяСтрокаНоменклатуры - ДанныеФормыСтруктура
&НаКлиенте
Процедура обновитьСуммуПоТекущейСтрокеНоменклатуры(Знач ткущаяСтрокаНоменклатуры)
    ткущаяСтрокаНоменклатуры.Сумма = ткущаяСтрокаНоменклатуры.Цена * ткущаяСтрокаНоменклатуры.Количество;
КонецПроцедуры

// Выполнят расчет суммы документа
&НаКлиенте
Процедура обновитьСуммуДокумента()
    ЭтотОбъект.Объект.СуммаДокумента = ЭтотОбъект.Объект.Товары.Итог("Сумма");
КонецПроцедуры

// Параметры:
//  номенклатура - СправочникСсылка.Номенклатура
// Возвращаемое значение:
//  - Число - Актуальная цена номенклатуры
//  - Неопределено - Если цена номенклатуры не определена
&НаКлиенте
Функция получитьЦенуНоменклатуры(Знач номенклатура)
    Возврат получитьЦенуНоменклатурыНаСервере(номенклатура, ЭтотОбъект.Объект.Дата);
КонецФункции

// Параметры:
//  номенклатура - СправочникСсылка.Номенклатура
//  период - Дата, МоментВремени, Граница
// Возвращаемое значение:
//  - Число - Актуальная цена номенклатуры
//  - Неопределено - Если цена номенклатуры не определена
&НаСервереБезКонтекста
Функция получитьЦенуНоменклатурыНаСервере(Знач номенклатура, Знач период)
    Возврат РегистрыСведений.ЦеныНоменклатуры.ПолучитьЦенуНоменклатуры(номенклатура, период);
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
